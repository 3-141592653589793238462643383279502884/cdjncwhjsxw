
 10  REM PRODUCT:OREGON TRAIL
 20  REM PROGRAM:BUY SUPPLIES
 30  REM PROGRAMMER:JOHN J KRENZ
 40  REM COPYRIGHT (C) MECC 1985
 50  REM LAST UPDATE 85/07/18 JJK
 100  LOMEM:39170: & APP,"COMMON.LIB"
 105  DEF  FN X(Z) =  INT (Z / 7) * 7: DEF  FN HI(Z) =  INT (Z / 256): DEF  FN LO(Z) = Z -  FN HI(Z) * 256: DEF  FN W(Z) =  VAL ( MID$ (WC$(ZO),(AM) * 2 + 1 + Z,1))
 110  POKE 919,0: GOSUB 40000:Q$ =  CHR$ (34):PN$ = "BUY SUPPLIES":QN$ = "OREGON TRAIL"
 120 MY = ( PEEK (913) +  PEEK (914) * 256) / 10:V = MY: GOSUB 200:K$ = Z$: FOR L = 904 TO 912: POKE L,0: NEXT : FOR L = 0 TO 5: READ M$(L): NEXT : FOR L = 0 TO 2: FOR L1 = 0 TO 1: READ W$(L,L1),SP$(L,L1): NEXT : NEXT : FOR L = 0 TO 5: READ I$(L): NEXT 
 199  POKE 919,1: GOTO 1000
 200 V =  INT (V * 100 + .5):Z$ = "0" +  STR$ (V):Z$ =  STR$ ( INT (V / 100)) + "." +  RIGHT$ (Z$,2): RETURN 
 220  & IN, - 9: PRINT "-";: & IN: RETURN 
 250  & GCP,ZX,ZY
 252  & CO,ZX,ZY: & CEL: PRINT  CHR$ (127):Z =  USR (5):Z = Z - 128: ON (Z < 49 OR Z > 53) AND Z <  > 32 GOTO 252:Z$ =  CHR$ (Z): IF Z = 32 THEN  RETURN 
 253  & CO,ZX,ZY: PRINT Z$ CHR$ (127)
 254 Z =  USR (5):Z = Z - 128: ON (Z = 127) OR (Z = 8) GOTO 252: IF Z = 13 THEN  RETURN 
 255  GOTO 254
 400  & IMAGE,8,150,140
 405  PRINT : PRINT : PRINT "There are 2 oxen in a yoke; I recommend at least 3 yoke.  I charge $40 a yoke.": PRINT : PRINT "How many yoke do you": PRINT "want? ";: & INP,1,"-19",1,Z$: POKE 905, VAL (Z$): RETURN 
 500  & IMAGE,2,150,140: PRINT "I recommend you take at least 200 pounds of food for each person in your family.  I see that you have 5 people in all.  You'll need flour, sugar, bacon, and coffee.  My price is 20 cents a pound.": PRINT 
 510  PRINT "How many pounds of food do you want? ";: & INP,4,"-09",1,Z$:Z =  VAL (Z$): IF Z > 2000 THEN  & BOX: & CO: PRINT : PRINT : PRINT "Your wagon may only carry 2000 pounds of food.": GOSUB 30000: & BOX: & CO,0,142: PRINT "Bill so far: "TB$: & CO: GOTO 500
 530  POKE 906, FN LO(Z): POKE 907, FN HI(Z): RETURN 
 600  & IMAGE,5,146,140: PRINT : PRINT "You'll need warm clothing in the mountains.  I recommend taking at least 2 sets of clothes per person.  Each set is $10.00."
 610  PRINT : PRINT "How many sets of clothes do you want? ";: & INP,2,"-09",1,Z$: POKE 908, VAL (Z$): RETURN 
 700  PRINT : PRINT : PRINT : & IMAGE,6,149,140: PRINT "I sell ammunition in boxes of 20 bullets.  Each box costs $2.00.": PRINT : PRINT "How many boxes do": PRINT "you want? ";: & INP,2,"-09",1,Z$: POKE 909, VAL (Z$): RETURN 
 800  & IMAGE,3,150,133: PRINT "It's a good idea to have a few spare parts for your wagon.  Here are the prices:": & IN,8: PRINT : FOR L = 0 TO 2: PRINT  SPC(1)SP$(L,0);: & GCP,,ZY: PRINT : & CO,109,ZY: PRINT "- $"SP$(L,1)" each": NEXT : & IN
 810  & DFW,1 AT 60,114,219,18: & WIND,1: FOR L = 0 TO 2: & BOX: & CO: PRINT "How many "SP$(L,0)"s? ";: & INP,1,"-09",1,Z$:Z =  VAL (Z$): POKE 910 + L,Z
 811  IF Z > 3 THEN  & CO: PRINT CF$;: PRINT "Your wagon may only carry 3 "SP$(L,0)"s.": & WIND,0: GOSUB 41100: & CO,60,184: PRINT "Bill so far: "TB$: & WIND,1: & BOX:L = L - 1
 815  NEXT : RETURN 
 900 V = 0: POKE 904,1: RETURN 
 910 V = 40 *  PEEK (905): RETURN 
 920 V = .2 * ( PEEK (907) * 256 +  PEEK (906)): RETURN 
 930 V = 10 *  PEEK (908): RETURN 
 940 V = 2 *  PEEK (909): RETURN 
 950 V = 10 * ( PEEK (910) +  PEEK (911) +  PEEK (912)): RETURN 
 1000  REM 
 1005  GOSUB 6000: GOSUB 33000: & DUN,"L0.PCK" AT PA,16: & CST,ST: GOSUB 2000:A$ = "buy": & DFT,7,37380: & WIND: & BOX,0,0,279,45: & BOX,50,0,279,191
 1010  GOSUB 4000: GOSUB 3000: IF Z THEN  & WIND: ON Z GOSUB 910,920,930,940,950:V = TB - V: GOSUB 200:TB$ = "$" + Z$: & BOX,50,35,279,191: & BOX,50,29,270,31,1: & DFW,1 AT 60,40,219,151: & WIND,1: & CO,0,142: PRINT "Bill so far: "TB$: & CO
 1015 Z = (Z = 0) * 6 + Z: ON Z GOSUB 400,500,600,700,800,5000: GOTO 1010
 1020  END 
 2000 X1 = 21:Y1 = 84:X2 = 256:Y2 = 140: GOSUB 41000: PRINT "Before leaving Independence, you should buy equipment and supplies.  You have $"K$" in cash, but you don't have to spend it all now.": GOSUB 41100: & PUT,X1,Y1,PA:Y1 = 112:X1 = 14: GOSUB 41000
 2010  PRINT "You can buy whatever you need at Matt's General Store.": GOSUB 41100: & WIND,0: PRINT CL$: PRINT : PRINT "Hello, I'm Matt.  So you're going to Oregon!  I can fix you up with what you need:"
 2020  & IMAGE,4,0,50: & DFW,1 AT 70,60,200,131: & WIND,1: & IN, - 16: PRINT : PRINT : PRINT "- a team of oxen to pull your wagon": PRINT : PRINT "- clothing for both summer and winter": GOSUB 30000: & BOX
 2030  & CO: PRINT "- plenty of food for the trip": PRINT : PRINT "- ammunition for your rifles": PRINT : PRINT "- spare parts for your wagon": & IN: GOSUB 30000: RETURN 
 3000  & QFH: & WIND: & BOX,50,0,279,191: & DFW,1 AT 60,10,219,181: & BOX,50,5,270,7,1: & BOX,50,47,270,49,1: & BOX,50,100,270,102,1: & WIND,1
 3010  PRINT "  Matt's General Store": PRINT "  Independence, Missouri": PRINT : & GCP,ZX,ZY:Z$ = M$( PEEK (902) - 2) + " " +  STR$ ( PEEK (903)) + ", 18" +  STR$ ( PEEK (901)): & CO,199 -  LEN (Z$) * 8,ZY: PRINT Z$: PRINT 
 3015  & GCP,ZX,ZY: & CO,ZX,ZY - 2: FOR L = 1 TO 5: ON L + 1 GOSUB 900,910,920,930,940,950: GOSUB 200: PRINT L". "I$(L);: & GCP,ZX,ZY: PRINT : & CO,135,ZY:Z0$ =  RIGHT$ ("   $" + Z$,8): PRINT Z0$: NEXT 
 3020  & WIND: & CO,111,106: PRINT "Total bill: " RIGHT$ ("   " + TB$,8):V = MY: GOSUB 200:K$ =  RIGHT$ ("    " + "$" + Z$,8): & CO,61,135: PRINT "Amount you have: "K$
 3030  & DFW,1 AT 70,150,180,41: & WIND,1: & CO,0,23: PRINT "Press SPACE BAR to leave store":Z =  USR (1): & CO: PRINT "Which item would you like to "A$"? ";: GOSUB 250: PRINT :Z =  VAL (Z$): RETURN 
 4000 Z = 0: FOR L = 1 TO 6: ON L GOSUB 900,910,920,930,940,950:Z = Z + V: NEXT :TB = Z:V = TB: GOSUB 200:TB$ = "$" + Z$: RETURN 
 5000  & DFW,1 AT 60,130,219,61: & WIND,1: PRINT CF$;: IF MY < TB THEN V = MY: GOSUB 200: PRINT "Okay, that comes to a total of "TB$".  But I see that you only have $"Z$".  We'd better go over the list again.": GOSUB 41100: & WIND,1: & BOX:A$ = "change": RETURN 
 5006  IF  NOT  PEEK (905) THEN  PRINT CF$;: PRINT "Don't forget, you'll need oxen to pull your wagon.": GOSUB 41100: & WIND,1: & BOX: RETURN 
 5010 MY = (MY - TB) * 10: POKE 913, FN LO(MY): POKE 914, FN HI(MY): & WIND,0: & BOX,50,0,279,191: & DFW,1 AT 60,60,190,131: & WIND,1
 5015  PRINT "Well then, you're ready to start.  Good luck!  You have a long and difficult journey ahead of you.": GOSUB 30000
 5020  GOSUB 33000: & DUN,"L0.PCK" AT PA,16: & CST,ST:X1 = 42:X2 = 231:Y1 = 160:Y2 = 180: GOSUB 41000: PRINT "Now loading the wagon...": & RFL,"EVENTS.IMA" AT 52200: & RFL,"MS0.BIN" AT 51944: & RFL,"PRAIRIE.IMA" AT 49152: & RNH,"OREGON TRAIL"
 6000  & DFT,7,49152: POKE 903,1: PRINT CL$: & IMAGE,3,10,0: & IMAGE,3,10,177
 6010  & DFW,1 AT 15,19,260,172: & WIND,1: PRINT : PRINT "It is 18" PEEK (901)".  Your jumping off place for Oregon is Independence, Missouri.  You must decide which month to leave Independence.": PRINT : PRINT 
 6020  FOR L = 1 TO 5: PRINT  SPC(3)L". "M$(L): NEXT : PRINT  SPC(3)L". Ask for advice": PRINT : PRINT : PRINT "What is your choice? ";: & INP,1,"-16",1,Z$: IF  VAL (Z$) = 6 THEN  GOSUB 6100: GOTO 6000
 6030  POKE 902, VAL (Z$) + 2: RETURN 
 6100  & WIND,0: & BOX,0,19,279,191: & IMAGE,3,10,164: & DFW,1 AT 15,25,255,172: & WIND,1
 6105  PRINT "You attend a public meeting held for "Q$"folks with the California - Oregon fever."Q$"  You're told:": PRINT : PRINT "If you leave too early, there won't be any grass for your oxen to eat.  ";
 6110  PRINT "If you leave too late, you may not get to Oregon before winter comes.  If you leave at just the right time, there will be green grass and the weather will still be cool.": GOSUB 30000: RETURN 
 29000  DATA February,March,April,May,June,July,average,wagon wheel,200,10,better-than-average,wagon axle,300,10,excellent,wagon tongue,400,10,Covered wagon,Oxen,Food,Clothing,Ammunition,Spare parts

